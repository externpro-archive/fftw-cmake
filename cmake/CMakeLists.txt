include(flags OPTIONAL)
set(VER_MAJ 3)
set(VER_MIN 3)
set(VER_PT 3)
set(VER_DOT ${VER_MAJ}.${VER_MIN}.${VER_PT})
if(NOT DEFINED FFTW_VER)
  set(FFTW_VER ${VER_DOT})
elseif(NOT ${FFTW_VER} STREQUAL ${VER_DOT})
  message(AUTHOR_WARNING "version passed in (${FFTW_VER}) doesn't match internal version (${VER_DOT})")
endif()
set(ver _${FFTW_VER})
set(verDir /${PROJECT_NAME}${ver})
set(targetsFile ${PROJECT_NAME}${ver}-targets)
set(fftwroot ${fftw_SOURCE_DIR})
get_filename_component(fftwroot ${fftwroot} ABSOLUTE)
include(macros.cmake)
if(MSVC)
  add_definitions(-D_CRT_SECURE_NO_WARNINGS)
  add_definitions(/Oi) # enable intrinsic functions
endif()
set(precisions double float long-double)
foreach(prec ${precisions})
  set(BENCHFFT_LDOUBLE)
  set(FFTW_LDOUBLE)
  set(BENCHFFT_SINGLE)
  set(FFTW_SINGLE)
  # NOTE: QUAD not yet supported
  set(BENCHFFT_QUAD)
  set(FFTW_QUAD)
  if(${prec} MATCHES "float")
    set(BENCHFFT_SINGLE 1)
    set(FFTW_SINGLE 1)
    set(pr "f")
  elseif(${prec} MATCHES "long-double")
    set(BENCHFFT_LDOUBLE 1)
    set(FFTW_LDOUBLE 1)
    set(pr "l")
  elseif(${prec} MATCHES "double")
    set(pr "")
  endif()
  set(lib_name libfftw${VER_MAJ}${pr}-${VER_MIN})
  set(cfg_dir ${CMAKE_BINARY_DIR}/${lib_name})
  set(exe_name bench${pr})
  if(NOT EXISTS ${cfg_dir})
    execute_process(COMMAND ${CMAKE_COMMAND} -E make_directory ${cfg_dir})
  endif()
  include(config.h.cmake)
  include(libfftw.cmake)
  include(bench.cmake)
endforeach()
if(GENERATE_CSCOPE)
  generate_cscope_db()
endif()
install(FILES ${fftwroot}/api/fftw3.h DESTINATION include${verDir}/fftw${VER_MAJ})
install(EXPORT ${targetsFile} DESTINATION lib/cmake)
